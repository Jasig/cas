description = "Apereo CAS Core Configuration"

dependencies {
    implementation libraries.springsecurity
    implementation libraries.mongo
    implementation libraries.jasypt
    implementation libraries.groovy
    implementation libraries.bouncycastle

    optional libraries.springbootconfigmetadata

    api project(":api:cas-server-core-api-configuration-model")
    api project(":api:cas-server-core-api-util")
    api project(":api:cas-server-core-api-authentication")
    api project(":api:cas-server-core-api-webflow")
}

task generateConfigurationMetadata(type: JavaExec) {
    doFirst {
        println "\nRunning CAS Spring configuration metadata processor in addition to the native " +
                "Spring Boot metadata processor, to create configuration " +
                "metadata for nested and indexed properties. The process starts with the build directory " +
                "at [$buildDir] for the CAS configuration module at [$projectDir.canonicalPath].\n"
    }
    main = "org.apereo.cas.configuration.metadata.ConfigurationMetadataGenerator"
    classpath = sourceSets.main.compileClasspath
    args buildDir, project(":api:cas-server-core-api-configuration-model").getProjectDir()
}

compileJava.dependsOn(processResources)

if (!Boolean.getBoolean("skipNestedConfigMetadataGen")) {
    jar.dependsOn(generateConfigurationMetadata)
}

